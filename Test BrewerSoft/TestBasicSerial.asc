100 ' ----------------------------
110 ' Simple Communication Program
120 ' ----------------------------
130 '
140 ' Note: Set "ECHO = -1" for Local ECHO
150 '       Set "ECHO -  0" for No Local ECHO
160 '
170 ECHO = 0: CLOSE
180 '
190 ' --------------------
200 ' *** Screen Setup ***
210 ' --------------------
220 KEY OFF: CLS
230 PRINT "Simple Communications Program"
240 PRINT "(Press [Esc] to Exit Program)": PRINT ""
250 '
260 ' ----------------------------------------------
270 ' *** Open Specified COM Port for Read/Write ***
280 ' ----------------------------------------------
290 ' Note: Use COM1 for Serial Port 1, COM2 for Serial Port 2
300 '
310 OPEN "COM1:4800,N,8,1,rs,cs,ds,cd" FOR RANDOM AS #1
320 '
330 ' --------------------------------
340 ' *** Display Local Keystrokes ***
350 ' --------------------------------
360 LocalKeystroke$ = INKEY$
370   IF LocalKeystroke$ = CHR$(27) THEN SYSTEM ' Exit on [Esc]
380   IF LocalKeystroke$ = "" THEN 450
390   IF ECHO THEN PRINT LocalKeystroke$;
400   PRINT #1, LocalKeystroke$;
410 '
420 ' -------------------------------
430 ' *** Display Remote Response ***
440 ' -------------------------------
450 IF EOF(1) THEN 360
460   RemoteCharacter$ = INPUT$(LOC(1), #1)
470   IF RemoteCharacter$ <> CHR$(13) THEN PRINT RemoteCharacter$;
480 '
490 ' ------------
500 ' *** Loop ***
510 ' ------------
520 GOTO 360