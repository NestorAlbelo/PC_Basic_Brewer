10 DEFINT A-Z
20 ON ERROR GOTO 0
30 DATA "Line bit rate",5,23,"300","(300..4800)"
40 DATA "Data bits",6,23,"7","(7 or 8)"
50 DATA "Parity type",7,23,"E","(E,O,or N)"
60 DATA "Host echoing",9,23,"Y","(Y or N)"
70 DATA "Screen width",10,23,"80","(40 or 80)"
80 DATA "Modem Command",12,18,"",""
90 FOR I=1 TO 6:READ TXT$(I),ROW(I),COL(I),FD$(I),F$(I):NEXT I
100 XON$=CHR$(17):XOFF$=CHR$(19)
110 DEF SEG=&H40:IF PEEK(1)=3 THEN MAX=6 ELSE MAX=5
120 DEF SEG:KEY OFF:SCREEN 0,0,0:CLS
130 ON ERROR GOTO 0:ID$="(TERM) - Terminal Emulator"
140 CLS:PRINT ID$:CLOSE:OPEN"COM1:,,,,CS,DS" AS 1:CLOSE
150 '
1000 ON KEY(1) GOSUB 1140:ON KEY(2) GOSUB 5110
1010 ON KEY(3) GOSUB 5200:ON KEY(4) GOSUB 5300
1020 FOR I=1 TO 2:KEY (I) ON:NEXT I:GOSUB 2000
1030 LOCATE 18,1,1,0,7:PRINT"Change <line,data>?";STRING$(VAL(FD$(5))-19," ")
1040 COL=0:D$="":LOCATE 18,21
1050 A$=INKEY$:IF A$="" THEN 1050
1060 A=ASC(A$):IF A=27 THEN 1030
1070 IF ASC(A$)=13 THEN 1100
1080 IF A<32 THEN 1050 ELSE PRINT A$;
1090 D$=D$+A$:GOTO 1050
1100 X=INSTR(1,D$,","):IF X<2 THEN 1030
1110 I=VAL(D$):IF (I<1) OR (I>MAX) THEN 1030
1120 FD$(I)=RIGHT$(D$,LEN(D$)-X):GOSUB 2000:GOTO 1030
1130 '
1140 RETURN 3000
1150 '
2000 WD=VAL(FD$(5)):ON ERROR GOTO 2010:WIDTH WD:GOTO 2020
2010 FD$(5)="40":WIDTH 40:RESUME 2020
2020 ON ERROR GOTO 0:CLS:K$="f1=Conv":GOSUB 6000:PRINT ID$:PRINT
2030 FOR I=1 TO MAX
2040 LOCATE ROW(I),1:PRINT I;TXT$(I)
2050 LOCATE ROW(I),COL(I):PRINT "["+FD$(I)+"] "+F$(I)
2060 NEXT I:RETURN
2070 '
3000 DV$="1:":IF MAX=6 AND VAL(FD$(1))>300 THEN DV$="2:"
3020 FD$(4)=LEFT$(FD$(4),1):ON ERROR GOTO 3050
3030 OPEN"COM"+DV$+FD$(1)+","+FD$(3)+","+FD$(2)+",,CS,DS" AS 1
3040 ON ERROR GOTO 3070:GOTO 4000
3050 LOCATE 23,1:PRINT"File open error":RESUME 1030
3060 '
3070 RESUME
3080 '
4000 OPEN "SCRN:" FOR OUTPUT AS 2
4010 ON COM(1) GOSUB 5400:COM(1) ON
4020 ON KEY(1) GOSUB 5000:ON KEY(2) GOSUB 5100:KEY(3) ON:KEY(4) ON
4030 CLS:GOSUB 7000:K$="f1=Menu":GOSUB 6000
4040 IF EOF(1) THEN 4070 ELSE A$=INPUT$(1,1)
4050 IF A$<>CHR$(10) THEN PRINT A$;
4060 GOSUB 5500
4070 A$=INKEY$:IF A$="" THEN 4040
4080 IF ASC(LEFT$(A$,1))=0 THEN 4040
4090 IF FD$(4)="n" OR FD$(4)="N" THEN PRINT A$;
4100 PRINT #1,A$;:GOTO 4040
4110 '
5000 COM(1)OFF:KEY(3)OFF:KEY(4)OFF:ON KEY(2) GOSUB 5110:CLOSE:CLS:RETURN 1000
5010 '
5100 PRINT #1,XON$;
5110 COM(1) OFF:CLOSE :KEY ON:CLS:ON ERROR GOTO 0:END
5120 '
5200 PRINT #1,CHR$(0);:RETURN
5210 '
5300 IF DV$="1:" THEN LCR=&H3FB ELSE LCR=&H2FB
5310 SD%=INP(LCR):OUT LCR,SD% OR &H40
5320 FOR D%=1 TO 100:NEXT D%:OUT LCR,SD%
5330 RETURN
5340 '
5400 IF LOC(1)>224 THEN PRINT #1,XOFF$;:PAUSE=-1
5410 RETURN
5500 IF LOC(1)<16 AND PAUSE THEN PRINT #1,XON$;:PAUSE=0
5510 RETURN
5520 '
6000 LOCATE 25,1:PRINT K$+" f2=Exit f3=Nul f4=Break";
6010 LOCATE 1,1,1,6,7:RETURN
6020 '
7000 IF FD$(6)<>"" THEN PRINT #1,CHR$(14)+FD$(6)
7010 RETURN
